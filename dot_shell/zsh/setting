#!/usr/bin/env zsh

###########################################################
# Common setting

bindkey -e
bindkey "\C-f" forward-word
bindkey "\C-b" backward-word
setopt auto_cd
autoload -U colors
colors
#WORDCHARS='*?_-.[]~=/&;!#$%^(){}<>'
WORDCHARS='_-|'

###########################################################
# Completion

## 補完機能有効にする
autoload -U compinit
compinit -u
zstyle ':completion:*' list-colors "${LS_COLORS}"
# 単語の入力途中でもTab補完を有効化
setopt complete_in_word
# 補完候補をハイライト
zmodload -i zsh/complist
zstyle ':completion:*:default' menu select=1
# 補完をhjklで移動
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
# キャッシュの利用による補完の高速化
zstyle ':completion::complete:*' use-cache true
# 補完リストの表示間隔を狭くする
setopt list_packed
# コマンドの打ち間違いを指摘してくれる
setopt correct
SPROMPT="correct: $RED%R$DEFAULT -> $GREEN%r$DEFAULT ? [Yes/No/Abort/Edit] => "

###########################################################
# History

HISTFILE=~/.zsh_history
HISTSIZE=100000
SAVEHIST=100000
setopt hist_ignore_dups     # 前と重複する行は記録しない
setopt share_history        # 同時に起動したzshの間でヒストリを共有する
setopt hist_reduce_blanks   # 余分なスペースを削除してヒストリに保存する
setopt HIST_IGNORE_SPACE    # 行頭がスペースのコマンドは記録しない
setopt HIST_IGNORE_ALL_DUPS # 履歴中の重複行をファイル記録前に無くす
setopt HIST_FIND_NO_DUPS    # 履歴検索中、(連続してなくとも)重複を飛ばす
setopt HIST_NO_STORE        # histroyコマンドは記録しない

###########################################################
# fzf setup
export FZF_DEFAULT_OPTS="--height 30% --reverse --border --ansi"
# fbr - checkout git branch
fbr() {
  local branches branch
    branches=$(git branch -vv) &&
    branch=$(echo "$branches" | fzf +m) &&
  git checkout $(echo "$branch" | awk '{print $1}' | sed "s/.* //")
}
# fbrm - checkout git branch (including remote branches)
fbrm() {
  local branches branch
  branches=$(git branch --all | grep -v HEAD) &&
  branch=$(echo "$branches" |
  fzf -d $(( 2 + $(wc -l <<< "$branches") )) +m) &&
  git checkout $(echo "$branch" | sed "s/.* //" | sed "s#remotes/[^/]*/##")
}
# fshow - git commit browser
fshow() {
  git log --graph --color=always \
      --format="%C(auto)%h%d %s %C(black)%C(bold)%cr" "$@" |
  fzf --no-sort --reverse --tiebreak=index --bind=ctrl-s:toggle-sort \
      --bind "ctrl-m:execute:
                (grep -o '[a-f0-9]\{7\}' | head -1 |
                xargs -I % sh -c 'git show --color=always % | less -R') << 'FZF-EOF'
                {}
FZF-EOF"
}
# fd - cd to selected directory
fd() {
  local dir
  dir=$(find ${1:-.} -path '*/\.*' -prune \
      -o -type d -print 2> /dev/null | fzf +m) &&
  cd "$dir"
}

###########################################################
# ghq + fzf on tmux (https://www.matsub.net/posts/2017/12/01/ghq-fzf-on-tmux)
function dev() {
  ghq_dir=$(ghq list | fzf)
  if [ $? -eq 0 ]; then
    moveto=$(ghq root)/$ghq_dir
    cd $moveto

    # rename session if in tmux
    if [[ ! -z ${TMUX} ]]
    then
      repo_name=${moveto##*/}
      tmux rename-session ${repo_name//./-}
      tmux rename-window ${repo_name//./-}
    fi
  fi
  unset ghq_dir
}
zle -N dev
bindkey '^G' dev

###########################################################
# starship
[ -x $(which starship) ] && eval "$(starship init zsh)"

###########################################################
# mise
[ -f $HOME/.local/bin/mise ] && eval "$($HOME/.local/bin/mise activate zsh)"
